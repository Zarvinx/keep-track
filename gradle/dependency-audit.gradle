apply plugin: 'org.owasp.dependencycheck'

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version?.toUpperCase()?.contains(it) }
    def stablePattern = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ stablePattern)
}

allprojects {
    apply plugin: 'com.github.ben-manes.versions'

    tasks.named("dependencyUpdates").configure {
        gradleReleaseChannel = "current"
        checkForGradleUpdate = true
        revision = "release"
        outputFormatter = "plain"
        outputDir = "$buildDir/reports/dependency-updates"
        reportfileName = "report"

        rejectVersionIf {
            isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
        }
    }
}

dependencyCheck {
    format = 'ALL'
    failBuildOnCVSS = 7.0f
    suppressionFile = "$rootDir/gradle/dependency-check-suppressions.xml"
    analyzers {
        assemblyEnabled = false
    }
}

tasks.register("dependencyAudit") {
    group = "verification"
    description = "Checks outdated dependencies and scans for known vulnerabilities."
    dependsOn(":app:dependencyUpdates")
    dependsOn("dependencyCheckAnalyze")
}
